terraform {
  required_providers {
    librenms = {
      source = "registry.terraform.io/jokelyo/librenms"
    }
  }
  required_version = ">= 1.11"
}

# See https://github.com/jokelyo/librenms-devenv which:
# - Provides a local LibreNMS instance in Docker for testing purposes
# - Includes a terraform plan for creating test GCP and LibreNMS resources
#
provider "librenms" {
  host = "http://localhost:8000"
  # providing token using LIBRENMS_TOKEN environment variable
  # token = "token"
}

resource "librenms_device" "compute_vm_1" {
  hostname  = "192.168.5.4"
  display   = "Compute VM 1"
  force_add = true

  snmp_v2c = {
    community = "5581eb63764a093c"
  }
}

resource "librenms_device" "compute_vm_2" {
  hostname  = "192.168.5.5"
  display   = "Compute VM 2"
  force_add = true

  location = librenms_location.test_location.name

  snmp_v2c = {
    community = "5581eb63764a093c"
  }
}

# create an example location
resource "librenms_location" "test_location" {
  name = "test location"

  fixed_coordinates = true
  latitude = -45.0862462
  longitude = 37.4220648
}

# create a service that checks http on the device
resource "librenms_service" "compute_vm_2_http" {
  device_id   = librenms_device.compute_vm_2.id
  name        = "HTTP3"
  type        = "http"

  ignore     = false
  parameters = "-C 30,14"
  target     = "192.168.1.1"

}

# create a static device group using the computed resource id
resource "librenms_devicegroup" "static_group_1" {
  name        = "static_group_1"
  description = "my static group"
  type        = "static"
  devices = [
    librenms_device.compute_vm_1.id,
    librenms_device.compute_vm_2.id,
  ]
}

# create a dynamic device group with devices that have a sysDescr containing "cloud"
resource "librenms_devicegroup" "test_dynamic_group_1" {
  name        = "test_dynamic_group_1"
  description = "Dynamic group for devices with 'cloud' in sysDescr"
  type        = "dynamic"

  rules = jsonencode({
    "condition" : "AND",
    "rules" : [
      {
        "id" : "devices.sysDescr",
        "field" : "devices.sysDescr",
        "operator" : "contains",
        "value" : "cloud"
      }
    ],
    "joins" : [],
    "valid" : true
  })
}

resource "librenms_alertrule" "cloud_device_down_icmp" {
  name  = "Cloud Devices Down (ICMP)"
  notes = "Alert when a device is down and the reason is ICMP"

  # Due to their complicated structure, I would recommend initially configuring the rule in the LibreNMS UI
  # and then exporting from the API to get the correct JSON format.
  builder = jsonencode({
    "condition" : "AND",
    "rules" : [
      {
        "id" : "macros.device_down",
        "field" : "macros.device_down",
        "type" : "integer",
        "input" : "radio",
        "operator" : "equal",
        "value" : "1"
      },
      {
        "id" : "devices.status_reason",
        "field" : "devices.status_reason",
        "type" : "string",
        "input" : "text",
        "operator" : "equal",
        "value" : "icmp"
      }
    ],
    "valid" : true
  })

  # you can also just use a serialized JSON string as it's represented in the LibreNMS API output
  # builder = "{\"condition\": \"AND\",....}"

  delay      = "11m"
  interval   = "5m"
  max_alerts = 1

  disabled = false
  severity = "critical"

  # defaults to all devices if devices is not defined
  # devices = [1, 2]

  # can also specify group IDs to limit the alert rule to specific device groups
  groups = [
    librenms_devicegroup.test_dynamic_group_1.id,
  ]
}

output "librenms_device_compute_vm_2" {
  value = librenms_device.compute_vm_2.hostname
}

output "librenms_devicegroup_static" {
  value = librenms_devicegroup.static_group_1
}

output "librenms_devicegroup_dynamic" {
  value = librenms_devicegroup.test_dynamic_group_1
}
